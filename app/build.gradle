plugins {
  id 'com.android.application'
  id 'kotlin-android'
  id 'kotlin-kapt'
  id 'kotlin-parcelize'
}

android {
  signingConfigs {
    Properties properties = new Properties()
    properties.load(new FileInputStream(file("../local.properties")))
    release {
      storeFile file(properties["storeFile"])
      storePassword properties["storePassword"]
      keyAlias properties["keyAlias"]
      keyPassword properties["keyPassword"]
    }
  }
  def ext = rootProject.ext
  compileSdkVersion ext.compileSdkVersion
  defaultConfig {
    applicationId ext.applicationId
    minSdkVersion ext.minSdkVersion
    targetSdkVersion ext.targetSdkVersion
    versionCode ext.versionCode
    versionName ext.versionName
    testInstrumentationRunner ext.testInstrumentationRunner
    signingConfig signingConfigs.release
  }
  buildTypes {
    debug {
      applicationIdSuffix ".debug"
    }
    release {
      minifyEnabled ext.minifyEnabled
      shrinkResources ext.shrinkResources
      proguardFiles(getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro')
    }
    mock {
      initWith debug
    }
  }
  kotlinOptions {
    jvmTarget = "1.8"
  }
}

dependencies {

  implementation project(':buisness-logic')
  implementation project(':navigation')

  def appDependencies = rootProject.ext.applicationDependencies
  implementation appDependencies.kotlin
  implementation appDependencies.appCompat
  implementation appDependencies.coordinatorLayout
  implementation appDependencies.constraintLayout
  implementation appDependencies.recyclerView
  implementation appDependencies.cicerone
  implementation appDependencies.moxy
  implementation appDependencies.moxyAndroidx
  implementation appDependencies.dagger
  implementation appDependencies.zxcvbn
  implementation appDependencies.androidSecurity
  implementation appDependencies.timber
  kapt appDependencies.daggerCompiler
  kapt appDependencies.moxyCompiler

  def testDependencies = rootProject.ext.testDependencies
  testImplementation testDependencies.junit
  testImplementation testDependencies.gson

  debugImplementation appDependencies.leakCanary
}